/-
Copyright (c) 2026 The Tau Ceti contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: The Tau Ceti contributors
-/
module

public import EpsilonEridani.AlgebraicTopology.EilenbergMacLane.Basic
public import EpsilonEridani.AlgebraicTopology.FundamentalGroup.HomotopyEquiv

/-!
# Asphericity and the `K(G, 1)` property are homotopy invariants

Both properties are stated at a base point, but neither depends on it
(`EpsilonEridani.IsAspherical.of_basepoint`, `EpsilonEridani.IsEilenbergMacLaneSpaceOne.of_basepoint`): an
aspherical space is path connected, so base-point change identifies its homotopy groups at any two
points. With that, homotopy invariance follows from the invariance of the homotopy groups
themselves, since a homotopy equivalence carries no base point with it.

These are the canonical invariance statements for both properties. In particular they cover a
homeomorphism `e : X ≃ₜ Y`, through `e.toHomotopyEquiv`, at any base point of `Y`.

## Main declarations

* `EpsilonEridani.IsAspherical.of_homotopyEquiv`: **asphericity is a homotopy invariant.**
* `EpsilonEridani.IsEilenbergMacLaneSpaceOne.of_homotopyEquiv`: **being a `K(G, 1)` space is a homotopy
  invariant.**

## References

Compare Section 1.B of [hatcher02].
-/

public section

namespace EpsilonEridani

open scoped Topology Topology.Homotopy ContinuousMap

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {x : X}

namespace IsAspherical

/-- **Asphericity is a homotopy invariant.** A space homotopy equivalent to an aspherical space
is aspherical, at every base point. -/
theorem of_homotopyEquiv (h : IsAspherical X x) (e : X ≃ₕ Y) (y : Y) : IsAspherical Y y := by
  let : PathConnectedSpace X := h.pathConnectedSpace
  refine IsAspherical.mk e.pathConnectedSpace fun n => ?_
  let : Subsingleton (π_ (n + 2) X x) := h.subsingleton_homotopyGroup n
  obtain ⟨φ⟩ := nonempty_homotopyGroupMulEquiv_of_homotopyEquiv (N := Fin (n + 2)) e x y
  exact φ.toEquiv.subsingleton_congr.mp inferInstance

end IsAspherical

namespace IsEilenbergMacLaneSpaceOne

variable {G : Type*} [Group G]

/-- **Being an Eilenberg--Mac Lane space of type `K(G, 1)` is a homotopy invariant.** -/
theorem of_homotopyEquiv (h : IsEilenbergMacLaneSpaceOne G X x) (e : X ≃ₕ Y) (y : Y) :
    IsEilenbergMacLaneSpaceOne G Y y := by
  let : PathConnectedSpace X := h.isAspherical.pathConnectedSpace
  obtain ⟨φ⟩ := e.nonempty_fundamentalGroupMulEquiv x y
  exact IsEilenbergMacLaneSpaceOne.mk (h.isAspherical.of_homotopyEquiv e y)
    (h.nonempty_fundamentalGroupMulEquiv.map fun f => φ.symm.trans f)

end IsEilenbergMacLaneSpaceOne

end EpsilonEridani
