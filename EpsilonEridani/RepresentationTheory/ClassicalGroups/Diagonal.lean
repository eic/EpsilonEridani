/-
Copyright (c) 2026 The Tau Ceti contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: The Tau Ceti contributors
-/
module

public import EpsilonEridani.RepresentationTheory.ClassicalGroups.Standard
public import EpsilonEridani.LinearAlgebra.Matrix.GeneralLinearGroup.Diagonal.Basic

/-!
# Diagonal elements in the standard representation

This file describes the action of the invertible diagonal matrices `EpsilonEridani.diagGL t` in the
standard representation. These elements are the concrete points of the diagonal torus used to
compute characters and weight spaces; the matrices themselves, and the torus they form, are in
`EpsilonEridani.LinearAlgebra.Matrix.GeneralLinearGroup.Diagonal.Basic`.

## Main statements

* `EpsilonEridani.stdRep_diagGL_apply_basisFun`: every standard basis vector is an eigenvector of a
  diagonal matrix.

## References

* [Classical groups roadmap](https://github.com/EpsilonEridaniProject/EpsilonEridaniRoadmap/blob/main/EpsilonEridaniRoadmap/RepresentationTheory/ClassicalGroups/README.md),
  Layer 1.
* W. Fulton and J. Harris, *Representation Theory: A First Course* (1991), Lecture 15.
-/

public section

open Matrix

universe u

namespace EpsilonEridani

variable {k : Type u} [CommRing k] {n : ℕ}

/-- The standard representation acts at `diagGL t` by coordinatewise multiplication. -/
@[simp↓]
theorem stdRep_diagGL_apply (t : Fin n → kˣ) (v : Fin n → k) (i : Fin n) :
    stdRep k n (diagGL t) v i = (t i : k) * v i := by
  rw [stdRep_apply_apply, diagGL_coe]
  exact Matrix.mulVec_diagonal _ _ _

/-- Every standard basis vector is an eigenvector for a diagonal matrix. -/
@[simp↓]
theorem stdRep_diagGL_apply_basisFun (t : Fin n → kˣ) (i : Fin n) :
    stdRep k n (diagGL t) (Pi.basisFun k (Fin n) i) =
      (t i : k) • Pi.basisFun k (Fin n) i := by
  ext j
  rw [stdRep_diagGL_apply]
  by_cases hji : j = i
  · subst j
    simp
  · simp [Pi.basisFun_apply, hji]

end EpsilonEridani
